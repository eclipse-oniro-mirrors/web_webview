# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

webview_root_path = "//base/web/webview"
native_source_path = rebase_path("$webview_root_path")
web_dir = "nweb_hap"
position = exec_script(
  "//foundation/communication/dsoftbus/run_shell_cmd.py",
  [ "cd $native_source_path; if [ -d $web_dir ]; then echo true; else echo false; fi" ],
  "value")
if (!position) {
  web_source = "prebuilts/arm/NWeb.hap"
} else {
  web_source = "../nweb_hap/NWeb.hap"
}

config("nweb_config") {
  include_dirs = [
    "include",
    "//utils/native/base/include",
    "//foundation/windowmanager/interfaces/innerkits/wm",
    "//foundation/multimodalinput/input/interfaces/native/innerkits/event/include",
    "//foundation/graphic/standard/frameworks/surface/include",
    "//foundation/graphic/standard/interfaces/innerkits/surface",
  ]

  cflags = [
    "-Wall",
    "-Werror",
    "-g3",
  ]
}

config("nweb_public_interface") {
  include_dirs = [ "//base/web/webview/ohos_nweb/include" ]
}

ohos_shared_library("libnweb") {
  configs = [ ":nweb_config" ]
  public_configs = [ ":nweb_public_interface" ]
  sources = [
    "src/nweb_helper.cpp",
    "src/nweb_input_event_consumer.cpp",
    "src/nweb_surface_adapter.cpp",
    "src/nweb_window_adapter.cpp",
  ]

  public_deps = [
    "//foundation/graphic/standard:libsurface",
    "//foundation/graphic/standard:libvsync_client",
    "//foundation/graphic/standard/rosen/modules/render_service_client:librender_service_client",
    "//foundation/windowmanager/wm:libwm",
  ]

  external_deps = [
    "hiviewdfx_hilog_native:libhilog",
    "input:libmmi-client",
    "ipc:ipc_core",
    "samgr_standard:samgr_proxy",
  ]

  part_name = "webview"
  subsystem_name = "web"
}

ohos_prebuilt_etc("nweb_hap") {
  source = web_source
  module_install_dir = "app"
  part_name = "webview"
  subsystem_name = "web"
}
